plugins {
    id 'maven-publish'
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'uniffi.zcash'
    compileSdk 33
    ndkVersion "26.0.10792818"

    defaultConfig {
        minSdk 26

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

cargo {
    module = '../uniffi-zcash-lib/lib'
    libname = 'uniffi_zcash'
    profile = 'release'
    targets = ['arm', 'arm64', 'x86', 'x86_64']
}

tasks.getByName("build") { releaseAar ->
    tasks.withType(org.gradle.api.publish.maven.tasks.AbstractPublishToMaven) { publishTask ->
        publishTask.dependsOn(releaseAar)
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'net.java.dev.jna:jna:5.10.0@aar'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

//task sourcesJar(type: Jar, dependsOn: bundleReleaseAar) {
//    archiveClassifier.set('sources')
//    from sourceSets.main.allSource
//}

publishing {
    publications {
        bar(MavenPublication) {
            artifact("$buildDir/outputs/aar/lib-release.aar")
            pom {
                groupId = "uniffi.android"
                artifactId = "zcash"
                version = "1.0.0"
                description = "The librustzcash Android FFI binding"
                url = "https://github.com/eigerco/uniffi-android-pkg"
                licenses {
                    license {
                        name.set("The MIT License")
                        url.set("https://github.com/eigerco/uniffi-android-pkg/blob/main/LICENSE")
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/eigerco/uniffi-android-pkg")
//            isAllowInsecureProtocol = true
            credentials(PasswordCredentials)
        }
    }
}
